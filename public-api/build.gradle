import io.swagger.codegen.config.CodegenConfigurator
import io.swagger.codegen.DefaultGenerator

def swaggerTemplateDir = 'src/main/resources'
def swaggerSourceFile = 'src/main/resources/public-api.yaml'
def swaggerTargetFolder = 'src/generated/java'

task generateApi {
  inputs.file("$projectDir/$swaggerSourceFile")
  outputs.dir("$projectDir/$swaggerTargetFolder")
  doLast {
    def config = new CodegenConfigurator()
    config.setInputSpec("file:///$projectDir/$swaggerSourceFile")
    config.setOutputDir("$projectDir")
    config.setTemplateDir("$projectDir/$swaggerTemplateDir")
    config.setLang('spring')
    config.setAdditionalProperties([
            'apiPackage'     : 'org.pmiops.workbench.publicapi',
            'modelPackage'   : 'org.pmiops.workbench.model',
            'sourceFolder'   : swaggerTargetFolder,
            'useTags'        : 'true',
            // Generates delegate interfaces; used to make method annotations work without
            // having to copy them to our implementations.
            'delegatePattern': 'true'
    ])
    new DefaultGenerator().opts(config.toClientOptInput()).generate()
  }
}

task generateApiClient {
  inputs.file("$projectDir/$swaggerSourceFile")
  outputs.dir("$projectDir/$swaggerTargetFolder")
  doLast {
    def config = new CodegenConfigurator()
    config.setInputSpec("file:///$projectDir/$swaggerSourceFile")
    config.setOutputDir("$projectDir")
    config.setTemplateDir("$projectDir/$swaggerTemplateDir")
    config.setLang('java')
    config.setAdditionalProperties([
            'invokerPackage'   : 'org.pmiops.workbench.publicapi.client',
            'modelPackage'   : 'org.pmiops.workbench.publicapi.client.model',
            'apiPackage'   : 'org.pmiops.workbench.publicapi.client.api',
            'sourceFolder'   : swaggerTargetFolder,
            'sourceFolder'     : swaggerTargetFolder,
            'library'          : 'okhttp-gson',
            'serializableModel': 'true',
            'dateLibrary'      : 'java11'
    ])
    new DefaultGenerator().opts(config.toClientOptInput()).generate()
  }
}


def workbenchApiFile = 'src/main/resources/private-workbench.yaml'

task generateWorkbenchClient {
  inputs.file("$projectDir/$workbenchApiFile")
  outputs.dir("$projectDir/$swaggerTargetFolder")
  doLast {
    def config = new CodegenConfigurator()
    config.setInputSpec("file:///$projectDir/$workbenchApiFile")
    config.setOutputDir("$projectDir")
    config.setTemplateDir("$projectDir/$swaggerTemplateDir")
    config.setLang('java')
    config.setAdditionalProperties([
            'invokerPackage'   : 'org.pmiops.workbench.privateworkbench',
            'modelPackage'     : 'org.pmiops.workbench.privateworkbench.model',
            'apiPackage'       : 'org.pmiops.workbench.privateworkbench.api',
            'sourceFolder'     : swaggerTargetFolder,
            'library'          : 'okhttp-gson',
            'serializableModel': 'true',
            'dateLibrary'      : 'java11'
    ])
    new DefaultGenerator().opts(config.toClientOptInput()).generate()
  }
}

task generate_appengine_yaml(type: Exec) {
  executable "ruby"
  args "libproject/generate_appengine_yaml.rb"
}

configurations {
  integrationCompile.extendsFrom testCompile
  integrationRuntime.extendsFrom testRuntime
  generatedCompile
  all {
    exclude group: 'com.google.guava', module:'guava-jdk5'
  }
}

buildscript {    // Configuration for building

  ext {
    GAE_VERSION = '2.0.5'
    GOOGLE_TRUTH_VERSION = '1.1.3'
    GSON_VERSION = '2.9.0'
    HIBERNATE_VERSION = '5.6.9.Final'
    SPRING_BOOT_VERSION = '2.7.1'
    SPRING_FRAMEWORK_VERSION = '5.3.21'
    JACKSON_DATABIND_VERSION = '2.13.3'
    JACKSON_VERSION = '2.13.3'
    SPRING_DEPENDENCY_MANAGEMENT_VERSION = '1.0.13.RELEASE'
    JACKSON_VERSION = '2.13.1'
    OKHTTP_VERSION = '2.7.5'
    SPRINGFOX_VERSION = '3.0.0'
    LIQUIBASE_VERSION = '4.12.0'
    SPRING_FRAMEWORK_VERSION = '5.3.21'
  }

  repositories {
    jcenter()    // Bintray's repository - a fast Maven Central mirror & more
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:${SPRING_BOOT_VERSION}"
    classpath 'com.google.cloud.tools:appengine-gradle-plugin:2.4.1'
    classpath 'gradle.plugin.org.hidetake:gradle-swagger-generator-plugin:2.12.0'
    classpath 'io.swagger:swagger-codegen:2.2.3'
    classpath 'org.owasp:dependency-check-gradle:6.0.1'
    classpath 'net.ltgt.gradle:gradle-apt-plugin:0.21'
    classpath "io.spring.gradle:dependency-management-plugin:${SPRING_DEPENDENCY_MANAGEMENT_VERSION}"
  }
}

ext['hibernate.version'] = '5.6.9.Final'

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

apply plugin: 'com.google.cloud.tools.appengine-appyaml'
apply plugin: 'org.springframework.boot'
apply plugin: 'org.hidetake.swagger.generator'
apply plugin: 'org.owasp.dependencycheck'
apply plugin: 'net.ltgt.apt-idea'
apply plugin: 'io.spring.dependency-management'

sourceSets {
  generated {
    compileClasspath = configurations.generatedCompile
  }
  main {
    compileClasspath += generated.output
    runtimeClasspath += generated.output
  }
  test {
    compileClasspath += generated.output
    runtimeClasspath += generated.output
  }
  integration {
    java {
      compileClasspath += main.output + test.output + generated.output
      runtimeClasspath += main.output + test.output + generated.output
      srcDir file('src/integration/java')
    }
  }
}

generateApi.dependsOn validateSwagger
ideaModule.dependsOn generateApi
compileGeneratedJava.dependsOn generateApi
ideaModule.dependsOn generateApiClient
compileGeneratedJava.dependsOn generateApiClient
ideaModule.dependsOn generateWorkbenchClient
compileGeneratedJava.dependsOn generateWorkbenchClient
classes.dependsOn generatedClasses
compileJava.dependsOn compileGeneratedJava

jar.dependsOn generate_appengine_yaml

task integration(type: Test) {
  group = LifecycleBasePlugin.VERIFICATION_GROUP
  testClassesDirs = sourceSets.integration.output.classesDirs
  classpath = sourceSets.integration.runtimeClasspath
  systemProperties = [
      'DB_API_BASE_PATH': System.getenv('DB_API_BASE_PATH')
  ]
  // Option to control size of stack trace:
  // jvmArgs '-XX:MaxJavaStackTraceDepth=10'
}

integration {
  // These tests should always run when requested as they are not hermetic.
  outputs.upToDateWhen { false }
}


clean.doFirst {
  delete("${projectDir}/$swaggerTargetFolder")
}

repositories {   // repositories for Jar's you access in your code
  jcenter()
  mavenCentral()
}

ext {
  mapstructVersion = '1.3.1.Final'
}

dependencies {
  // To show the dependency tree, try: ./project.rb gradle dependencies --configuration compile
  compile 'mysql:mysql-connector-java:8.0.29'

  // https://mvnrepository.com/artifact/org.springframework.security/spring-security-config
  compile group: 'org.springframework.security', name: 'spring-security-config', version: '5.6.2'
  compile 'com.google.guava:guava:26.0-jre'

  compile 'com.google.cloud.sql:mysql-socket-factory:1.6.1'
  compile "com.google.appengine:appengine-api-1.0-sdk:${GAE_VERSION}"
  compile("org.springframework.boot:spring-boot-starter-web:${SPRING_BOOT_VERSION}") {
    exclude module: 'spring-boot-starter-tomcat'
    exclude group: 'org.slf4j', module: 'jul-to-slf4j'
  }
  compile("org.springframework.boot:spring-boot-starter-data-jpa:${SPRING_BOOT_VERSION}") {
    exclude module: 'spring-boot-starter-tomcat'
    exclude group: 'com.zaxxer', module: 'HikariCP'
    exclude group: 'org.slf4j', module: 'jul-to-slf4j'
  }

  compile "com.google.code.gson:gson:${GSON_VERSION}"
  compile 'com.google.api-client:google-api-client-appengine:1.35.1'
  compile("org.springframework.boot:spring-boot-starter-actuator:${SPRING_BOOT_VERSION}") {
    exclude module: 'spring-boot-starter-tomcat'
    exclude group: 'org.slf4j', module: 'jul-to-slf4j'
  }

  compile "org.springframework.retry:spring-retry"
  compile "org.springframework.security:spring-security-core"
  compile "org.springframework.security:spring-security-web"

  // https://security.snyk.io/vuln/SNYK-JAVA-ORGBOUNCYCASTLE-2841508
  compile 'org.bouncycastle:bcprov-jdk15on:1.70'
  compile "org.springframework:spring-aop:${SPRING_FRAMEWORK_VERSION}"
  compile "org.springframework:spring-aspects:${SPRING_FRAMEWORK_VERSION}"
  compile "org.springframework:spring-beans:${SPRING_FRAMEWORK_VERSION}"
  compile "org.springframework:spring-context:${SPRING_FRAMEWORK_VERSION}"
  compile "org.springframework:spring-core:${SPRING_FRAMEWORK_VERSION}"
  compile "org.springframework:spring-expression:${SPRING_FRAMEWORK_VERSION}"
  compile "org.springframework:spring-jcl:${SPRING_FRAMEWORK_VERSION}"
  compile "org.springframework:spring-jdbc:${SPRING_FRAMEWORK_VERSION}"
  compile "org.springframework:spring-orm:${SPRING_FRAMEWORK_VERSION}"
  compile "org.springframework:spring-test:${SPRING_FRAMEWORK_VERSION}"
  compile "org.springframework:spring-tx:${SPRING_FRAMEWORK_VERSION}"
  compile "org.springframework:spring-web:${SPRING_FRAMEWORK_VERSION}"
  compile "org.springframework:spring-webmvc:${SPRING_FRAMEWORK_VERSION}"

  compile "org.hibernate:hibernate-core:${HIBERNATE_VERSION}"

  compile('org.apache.tomcat:tomcat-jdbc:10.0.20')
  compile("org.springframework.boot:spring-boot-starter-tomcat:${SPRING_BOOT_VERSION}")
  compile("org.springframework.boot:spring-boot-starter-jdbc:${SPRING_BOOT_VERSION}") {
    exclude group: 'com.zaxxer', module: 'HikariCP'
  }
  compile("org.springframework.boot:spring-boot-starter-tomcat")
  compile("org.springframework.boot:spring-boot-starter-jdbc") {
    exclude group: 'com.zaxxer', module: 'HikariCP'
  }

  compile 'ch.qos.logback:logback-classic:1.2.11'
  compile "com.fasterxml.jackson.core:jackson-annotations:${JACKSON_VERSION}"
  compile "com.fasterxml.jackson.core:jackson-core:${JACKSON_VERSION}"
  compile "com.fasterxml.jackson.core:jackson-databind:${JACKSON_DATABIND_VERSION}"
  compile "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${JACKSON_VERSION}"
  compile "com.squareup.okhttp:logging-interceptor:${OKHTTP_VERSION}"
  compile "com.squareup.okhttp:okhttp:${OKHTTP_VERSION}"
  compile 'joda-time:joda-time:2.10'
  compile 'javax.inject:javax.inject:1'
  compile 'io.swagger:swagger-annotations:1.6.6'
  compile 'org.apache.commons:commons-lang3:3.12.0'
  compile 'com.google.guava:guava:31.0-jre'
  compile 'io.netty:netty-common:4.1.77.Final'
  compile "com.google.cloud:google-cloud-bigquery:2.13.2"

  implementation "org.mapstruct:mapstruct:${mapstructVersion}"
  annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
  testAnnotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

  compile 'com.google.api-client:google-api-client-appengine:1.35.1'
  compile 'com.auth0:java-jwt:3.19.2'

  compile 'com.google.auth:google-auth-library-appengine:1.7.0'
  compile 'com.google.auth:google-auth-library-oauth2-http:1.7.0'

  // https://github.com/GoogleCloudPlatform/google-cloud-java/issues/1502
  compile 'org.json:json:20220320'
  compile "org.springframework.boot:spring-boot-starter-validation:${SPRING_BOOT_VERSION}"

  testCompile 'junit:junit:4.12'
  testCompile 'org.mockito:mockito-core:3.11.2'
  testCompile "com.google.truth:truth:${GOOGLE_TRUTH_VERSION}"


  testCompile("org.springframework.boot:spring-boot-starter-test:${SPRING_BOOT_VERSION}") {
    exclude group: 'org.junit', module: 'junit'
    exclude group: 'org.mockito', module: 'mockito-core'
    exclude group: 'org.mockito', module: 'mockito-all'
  }

  testCompile 'com.h2database:h2:1.4.194'
  testCompile "org.liquibase:liquibase-core:${LIQUIBASE_VERSION}"
  testCompile 'org.bitbucket.radistao.test:before-after-spring-test-runner:0.1.0'

  // For Swagger generation. These should include dependencies found in the Swagger codegen
  // example here:
  // https://github.com/swagger-api/swagger-codegen/blob/v2.2.3/samples/client/petstore/spring-stubs/pom.xml
  generatedCompile "org.springframework.boot:spring-boot-starter-data-rest:${SPRING_BOOT_VERSION}"
  generatedCompile "org.springframework.boot:spring-boot-starter-validation:${SPRING_BOOT_VERSION}"
  generatedCompile "io.springfox:springfox-swagger2:${SPRINGFOX_VERSION}"
  generatedCompile "io.springfox:springfox-swagger-ui:${SPRINGFOX_VERSION}"
  generatedCompile "com.squareup.okhttp:okhttp:${OKHTTP_VERSION}"
  generatedCompile "com.squareup.okhttp:logging-interceptor:${OKHTTP_VERSION}"
  generatedCompile "com.google.code.gson:gson:${GSON_VERSION}"
  generatedCompile 'joda-time:joda-time:2.10'
  generatedCompile "com.fasterxml.jackson.datatype:jackson-datatype-joda:${JACKSON_VERSION}"
  generatedCompile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
}

swaggerSources {
  workbench {
    inputFile = file("$projectDir/$swaggerSourceFile")
  }
}

tasks.withType(Test) {
  testLogging {
    // Causes the correct line to be reported on an exception.
    exceptionFormat "full"
  }
  def verboseTestLogging=project.properties['verboseTestLogging'] ?: 'no'
  // To debug def verboseTestLogging='yes'
  if (verboseTestLogging == 'yes') {
    testLogging {
      events "passed", "skipped", "failed", "standardOut", "standardError"
    }
  } else {
    testLogging {
      events "passed", "skipped", "failed"
    }
  }
}

test {
  // Starting with a smaller minimum seems to help this task use memory more slowly.
  // Increase max heap size from default of 1024M in order to avoid OOM errors.
  // When Gradle runs out of memory, it fails with this message:
  //   Process 'Gradle Test Executor 1' finished with non-zero exit value 137
  // and literally nothing else in terms of helpful debugging information.
  minHeapSize = '128m'
  maxHeapSize = '2048m'
}

appengine {  // App Engine tasks configuration
  tools {
    cloudSdkHome = "/google-cloud-sdk"
  }

  // dmohs: You may see this message [1], but don't implement the suggested fix because it breaks
  // Spring.
  // [1] https://github.com/GoogleCloudPlatform/app-gradle-plugin/issues/100
  // stage {
  //    enableJarClasses = true
  // }

  deploy {   // deploy configuration
    stopPreviousVersion = true  // default - stop the current version
    promote = true              // default - & make this the current version
    // TODO(danrodney)
    //account = System.properties("account")
    //project = System.properties("project")
  }
}

bootRun {
  environment = [
          'GOOGLE_APPLICATION_CREDENTIALS': file("$rootDir/sa-key.json").getAbsolutePath()
  ]
}

group = 'org.pmiops.allofus.workbench'
version = '0.1.0'          // Version in generated output

sourceCompatibility = 11
targetCompatibility = 11
