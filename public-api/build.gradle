import org.gradle.api.tasks.JavaExec

// Runs before all tasks. Sets up properties and dependencies for the build
// process itself.
buildscript {
    // External properties on the default project. Values declared in ext blocks
    // outside of the buildscript block aren't usable here.
    ext {
        GAE_VERSION = '2.0.26'
        GOOGLE_TRUTH_VERSION = '1.1.3'
        GSON_VERSION = '2.9.1'
        HIBERNATE_VERSION = '6.4.4.Final'
        JACKSON_VERSION = '2.16.1'
        LIQUIBASE_VERSION = '4.27.0'
        LOGBACK_VERSION = '1.5.5'
        MAPSTRUCT_VERSION = '1.5.5.Final'
        OKHTTP_VERSION = '4.12.0'
        SPRINGFOX_VERSION = '3.0.0'
        SPRING_BOOT_VERSION = '3.2.4'
        SPRING_FRAMEWORK_VERSION = '6.1.5'
        SPRING_SECURITY_VERSION = '6.2.3'
        SWAGGER_3_CODEGEN_VERSION = '3.0.52'
    }

    repositories {
        jcenter()    // Bintray's repository - a fast Maven Central mirror & more
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            // for Terra service clients (currently sam-client)
            url 'https://broadinstitute.jfrog.io/artifactory/libs-release-local'
        }
        mavenCentral()
    }

    dependencies {
        classpath 'com.google.cloud.tools:appengine-gradle-plugin:2.4.5'

        classpath "com.google.http-client:google-http-client:1.41.1"
        // Note: if you plan to upgrade the version of swagger-codegen beyond 2.2.3, be aware of two
        // implicit dependencies: (1) the set of "generatedCompile" Gradle dependencies need to be updated
        // to match the modules imported by the Swagger codegen templates, and (2) some template files
        // are overridden with forked templates within this repo; they will likely need to be re-copied from
        // the newer release version and have AoU-specific modifications re-applied.

        classpath "junit:junit:4.12"

    }
}


plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'war'

    id 'org.springframework.boot' version '3.2.4'
    id 'org.owasp.dependencycheck' version '7.4.4'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'org.hidetake.swagger.generator' version '2.19.2'

    id 'com.google.cloud.tools.appengine-appenginewebxml' version '2.4.2'
}



def db_host = System.getenv("DB_HOST")
def db_port = System.getenv("DB_PORT")
def databrowser_db_user = System.getenv("DATABROWSER_DB_USER")
def databrowser_db_password = System.getenv("DATABROWSER_DB_PASSWORD")
def project_name = System.getenv("PROJECT")

def dbProperties = [
        'spring.datasource.driver-class-name': 'com.mysql.cj.jdbc.Driver',
        // Our database isn't configured to allow outside connections (which is good), so our connections
        // are either local or through the proxy, which encrypts all traffic.
        'spring.datasource.url': "jdbc:mysql://${db_host}:${db_port}/databrowser?useSSL=false",
        'spring.datasource.username': '${databrowser_db_user}',
        'spring.datasource.password': '${databrowser_db_password}'
]

if (JavaVersion.current() != JavaVersion.VERSION_17) {
    throw new GradleException("This build must be run with Java 17. See developer-system-initialization.md")
}

task generate_local_appengine_web_xml(type: Exec) {
    executable "ruby"
    args "libproject/generate_appengine_web_xml.rb"
}

configurations {
    integrationCompile.extendsFrom testImplementation
    integrationRuntime.extendsFrom testRuntime

    integrationImplementation.extendsFrom implementation
    integrationRuntimeOnly.extendsFrom runtimeOnly

    __swaggerCodegenV3__

    generatedCompile
    all {
        exclude group: 'com.google.guava', module:'guava-jdk5'
    }
    toolsImplementation.extendsFrom implementation
    toolsRuntimeOnly.extendsFrom runtimeOnly
}

ext['hibernate.version'] = '6.4.4.Final'

// Shared variables for Swagger codegen. Use "ext" rather than "def" to allow
// access globally, including from within helper functions.
ext.SWAGGER_CODEGEN_BASE_PKG = 'org.pmiops.workbench'
ext.SWAGGER_3_CODEGEN_DIR = "$buildDir/swagger3"
// This path is the Java sources root for finding any overridden Swagger-codegen
// templates we may be using.
ext.SWAGGER_TEMPLATE_DIR = "$projectDir/src/main/resources"
// Raw options passed to the Swagger codegen CLI.
ext.SWAGGER_3_RAW_OPTS = [
        // By default, the ignore file within the output directory is used. Override
        // as we don't track the output directory in source.
        '--ignore-file-override', "$projectDir/.swagger3-codegen-ignore"
]

sourceSets {
    generated {
        compileClasspath = configurations.generatedCompile
        java {
            srcDir SWAGGER_3_CODEGEN_DIR
        }
    }
    main {
        compileClasspath += generated.output
        runtimeClasspath += generated.output
    }
    test {
        compileClasspath += generated.output
        runtimeClasspath += generated.output
    }
    integration {
        java {
            compileClasspath += main.output + test.output + generated.output
            runtimeClasspath += main.output + test.output + generated.output
            srcDir file('src/integration/java')
        }
    }
    tools {
        compileClasspath += main.output + generated.output
        runtimeClasspath += main.output + generated.output

        resources {
            srcDir "tools/src/main/resources"
        }
        java {
            srcDirs = ['tools/src/main/java', 'src/main/java', 'src/generated/java']
            includes = ['org/pmiops/workbench/tools/**/*.java',
                        'org/pmiops/workbench/db/**/*.java',
                        // In general, command-line tools won't need access to
                        // org.pmiops.workbench.model (Swagger-generated classes),
                        // so we selectively whitelist as needed.
                        'org/pmiops/workbench/config/CdrBigQuerySchemaConfig.java',
                        'org/pmiops/workbench/config/WorkbenchConfig.java',
                        'org/pmiops/workbench/model/Authority.java',
                        'org/pmiops/workbench/model/BlockscoreVerificationStatus.java',
                        'org/pmiops/workbench/model/BillingProjectStatus.java',
                        'org/pmiops/workbench/model/ContactEmailTakenResponse.java',
                        'org/pmiops/workbench/model/DataAccessLevel.java',
                        'org/pmiops/workbench/model/EmailVerificationStatus.java',
                        'org/pmiops/workbench/model/ErrorCode.java',
                        'org/pmiops/workbench/model/ErrorResponse.java',
                        'org/pmiops/workbench/model/WorkspaceAccessLevel.java',
                        'org/pmiops/workbench/model/CohortStatus.java',
                        'org/pmiops/workbench/model/CohortReview.java',
                        'org/pmiops/workbench/model/Domain.java',
                        'org/pmiops/workbench/model/PageVisit.java',
                        'org/pmiops/workbench/model/ParticipantCohortStatus.java',
                        'org/pmiops/workbench/model/ReviewStatus.java',
                        'org/pmiops/workbench/model/UnderservedPopulationEnum.java',
                        'org/pmiops/workbench/model/AnnotationType.java']
            excludes = ['org/pmiops/workbench/db/dao/ConceptSetService.java',
                        'org/pmiops/workbench/db/dao/UserService.java',
                        'org/pmiops/workbench/db/dao/WorkspaceService.java',
                        'org/pmiops/workbench/db/dao/WorkspaceServiceImpl.java',
                        'org/pmiops/workbench/db/dao/ParticipantCohortStatusDao.java',
                        'org/pmiops/workbench/db/dao/ParticipantCohortStatusDaoImpl.java',
                        'org/pmiops/workbench/db/dao/UserRecentResourceServiceImpl.java',
                        'org/pmiops/workbench/cohortreview/util/*.java',
                        'org/pmiops/workbench/model/Filter.java',
                        'org/pmiops/workbench/model/Operator.java',
                        'org/pmiops/workbench/model/ParticipantCohortStatusColumns.java',
                        'org/pmiops/workbench/model/SortOrder.java',
                        'org/pmiops/workbench/db/dao/CohortService.java',
                        'org/pmiops/workbench/db/dao/ParticipantCohortStatusDaoCustom.java']
        }
    }
}

//generateApi.dependsOn validateSwagger
//ideaModule.dependsOn generateApi
//project.tasks.compileGeneratedJava.dependsOn generateApi
//ideaModule.dependsOn generateApiClient
//project.tasks.compileGeneratedJava.dependsOn generateApiClient
//ideaModule.dependsOn generateWorkbenchClient
//project.tasks.compileGeneratedJava.dependsOn generateWorkbenchClient
classes.dependsOn generatedClasses

ideaModule.dependsOn(generateSwaggerCode)

compileGeneratedJava.dependsOn(generateSwaggerCode)

war.dependsOn compileGeneratedJava
war.dependsOn generate_local_appengine_web_xml


// Run this via ./project.rb update-cloud-config or (locally) docker-compose run update-config,
// which is automatically run during api/project.rb dev-up.
task loadConfig(type: JavaExec) {
    classpath sourceSets.tools.runtimeClasspath
    main = "org.pmiops.workbench.tools.ConfigLoader"
    systemProperties = dbProperties
    if (project.hasProperty("config_key") && project.hasProperty("config_file")) {
        args config_key, config_file
    }
}

// This task is called from:
// - docker-compose.yaml#update-cdr-versions
// - devstart.rb > update_cdr_versions_for_project (called by run-local-migrations)
task updateCdrConfig(type: JavaExec) {
    classpath sourceSets.tools.runtimeClasspath
    main = "org.pmiops.workbench.tools.UpdateCdrVersions"
    systemProperties = dbProperties
    if (project.hasProperty("appArgs")) {
        args Eval.me(appArgs)
    }
}

task compileIntegration(type: JavaCompile) {
    sourceSets.integration.java.srcDirs = ['src/integration/java']
    classpath = sourceSets.main.compileClasspath
}

task integration(type: Test) {
    group = LifecycleBasePlugin.VERIFICATION_GROUP
    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath
    systemProperties = [
            'DB_API_BASE_PATH': System.getenv('DB_API_BASE_PATH')
    ]
    // Option to control size of stack trace:
    // jvmArgs '-XX:MaxJavaStackTraceDepth=10'
}

integration {
    // These tests should always run when requested as they are not hermetic.
    outputs.upToDateWhen { false }
}

task integrationTest(type: Test) {
    group = LifecycleBasePlugin.VERIFICATION_GROUP
    description = 'Run integration test suite.'
    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath
    systemProperties = [
            'DB_API_BASE_PATH': System.getenv('DB_API_BASE_PATH')
    ]
    // These tests should always run when requested since they consume and produce side-effects.
    outputs.upToDateWhen { false }

    useJUnitPlatform()

    // Option to control size of stack trace:
    // jvmArgs '-XX:MaxJavaStackTraceDepth=10'
}

clean.doFirst {
    delete("${buildDir}/swagger3")
}

repositories {   // repositories for Jar's you access in your code
    jcenter()
    mavenCentral()
}

ext {
    mapstructVersion = '1.3.1.Final'
}

// To show the dependency tree, try: ./project.rb gradle dependencies --configuration compile
dependencies {
    annotationProcessor "org.mapstruct:mapstruct-processor:$project.ext.MAPSTRUCT_VERSION"

    __swaggerCodegenV3__ "io.swagger.codegen.v3:swagger-codegen-cli:$project.ext.SWAGGER_3_CODEGEN_VERSION"

    implementation "ch.qos.logback:logback-classic:$project.ext.LOGBACK_VERSION"
    implementation "ch.qos.logback:logback-core:$project.ext.LOGBACK_VERSION"
    implementation "com.fasterxml.jackson.core:jackson-annotations:$project.ext.JACKSON_VERSION"
    implementation "com.fasterxml.jackson.core:jackson-core:$project.ext.JACKSON_VERSION"
    implementation "com.fasterxml.jackson.core:jackson-databind:$project.ext.JACKSON_VERSION"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$project.ext.JACKSON_VERSION"
    implementation "com.github.java-json-tools:json-patch:1.13"
    implementation "com.github.rholder:guava-retrying:2.0.0"
    implementation 'org.javers:javers-core:5.15.0'
    implementation "com.squareup.okhttp3:logging-interceptor:$project.ext.OKHTTP_VERSION"
    implementation "com.squareup.okhttp3:okhttp:$project.ext.OKHTTP_VERSION"

    implementation 'io.swagger:swagger-annotations:1.6.7'
    implementation "org.liquibase:liquibase-core:$project.ext.LIQUIBASE_VERSION"
    implementation 'com.mysql:mysql-connector-j:8.3.0'
    implementation "org.apache.commons:commons-collections4:4.4"
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.apache.commons:commons-text:1.10.0'
    implementation "org.apache.httpcomponents.client5:httpclient5:5.3.1"
    implementation "org.springframework.boot:spring-boot-starter-validation:$project.ext.SPRING_BOOT_VERSION"
    implementation "commons-codec:commons-codec:1.15"
    implementation 'com.auth0:java-jwt:3.19.2'
    implementation 'io.netty:netty-common:4.1.82.Final'
    implementation 'org.json:json:20231013'

    // TODO: consider switching google deps to the BOM.  https://cloud.google.com/java/docs/bom

    implementation("com.google.appengine:appengine-api-1.0-sdk:$project.ext.GAE_VERSION") {
        exclude group: 'com.google.api-client', module: 'google-api-client-servlet'
    }
    implementation 'com.google.apis:google-api-services-admin-directory:directory_v1-rev20220919-2.0.0'
    implementation 'com.google.apis:google-api-services-cloudbilling:v1-rev20220908-2.0.0'
    implementation 'com.google.apis:google-api-services-cloudresourcemanager:v3-rev20220925-2.0.0'
    implementation 'com.google.apis:google-api-services-oauth2:v2-rev20200213-2.0.0'
    implementation 'com.google.apis:google-api-services-iam:v1-rev20220825-2.0.0'
    implementation 'com.google.api-client:google-api-client-appengine:2.2.0'
    implementation 'com.google.auth:google-auth-library-appengine:1.23.0'
    implementation 'com.google.auth:google-auth-library-oauth2-http:1.23.0'
    implementation 'com.google.cloud.sql:mysql-socket-factory:1.7.0'
    implementation 'com.google.cloud:google-cloud-bigquery:2.25.0'
    implementation 'com.google.cloud:google-cloud-iamcredentials:2.3.6'
    implementation 'com.google.cloud:google-cloud-logging:3.11.5'
    implementation 'com.google.cloud:google-cloud-monitoring:3.40.0'
    implementation 'com.google.cloud:google-cloud-trace:2.39.0'
    implementation 'com.google.cloud:google-cloud-storage:2.13.0'
    implementation 'com.google.cloud:google-cloud-tasks:2.3.11'
    implementation "com.google.code.gson:gson:$project.ext.GSON_VERSION"
    implementation 'com.google.guava:guava:32.0.1-jre'
    implementation 'com.google.oauth-client:google-oauth-client-jetty:1.34.1'
    implementation 'com.google.protobuf:protobuf-java:3.25.3'
    implementation 'com.googlecode.owasp-java-html-sanitizer:owasp-java-html-sanitizer:20211018.2'

    implementation "org.hibernate.orm:hibernate-core:$project.ext.HIBERNATE_VERSION"
    implementation "org.hibernate.orm:hibernate-community-dialects:$project.ext.HIBERNATE_VERSION"

    implementation('org.apache.tomcat:tomcat-jdbc:10.0.23')
    implementation("org.springframework.boot:spring-boot-starter-tomcat:$project.ext.SPRING_BOOT_VERSION")
    implementation("org.springframework.boot:spring-boot-starter-jdbc:$project.ext.SPRING_BOOT_VERSION") {
        exclude group: 'com.zaxxer', module: 'HikariCP'
    }

    implementation("org.springframework.boot:spring-boot-starter-data-jpa:$project.ext.SPRING_BOOT_VERSION") {
        exclude module: 'spring-boot-starter-tomcat'
        exclude group: 'org.slf4j', module: 'jul-to-slf4j'
    }

    implementation("org.springframework.boot:spring-boot-starter-web:$project.ext.SPRING_BOOT_VERSION") {
        exclude module: 'spring-boot-starter-tomcat'
        exclude group: 'org.slf4j', module: 'jul-to-slf4j'
    }

    implementation "org.springframework.boot:spring-boot-starter-security:$project.ext.SPRING_BOOT_VERSION"

    implementation "org.springframework.retry:spring-retry"
    implementation "org.springframework.security:spring-security-core:$project.ext.SPRING_SECURITY_VERSION"
    implementation "org.springframework.security:spring-security-config:$project.ext.SPRING_SECURITY_VERSION"
    implementation "org.springframework.security:spring-security-crypto:$project.ext.SPRING_SECURITY_VERSION"
    implementation "org.springframework.security:spring-security-web:$project.ext.SPRING_SECURITY_VERSION"

    // updated versions of transitive dependencies to resolve vulnerabilities

    // https://security.snyk.io/vuln/SNYK-JAVA-ORGBOUNCYCASTLE-6612984
    // https://security.snyk.io/vuln/SNYK-JAVA-ORGBOUNCYCASTLE-6613076
    // https://security.snyk.io/vuln/SNYK-JAVA-ORGBOUNCYCASTLE-6613079
    implementation 'org.bouncycastle:bcprov-jdk18on:1.78'
    // https://spring.io/blog/2022/03/31/spring-framework-rce-early-announcement
    implementation "org.springframework:spring-aop:$project.ext.SPRING_FRAMEWORK_VERSION"
    implementation "org.springframework:spring-aspects:$project.ext.SPRING_FRAMEWORK_VERSION"
    implementation "org.springframework:spring-beans:$project.ext.SPRING_FRAMEWORK_VERSION"
    implementation "org.springframework:spring-context:$project.ext.SPRING_FRAMEWORK_VERSION"
    implementation("org.springframework:spring-core:$project.ext.SPRING_FRAMEWORK_VERSION") {
        exclude group: 'org.springframework', module: 'spring-jcl'
    }
    implementation "org.springframework:spring-expression:$project.ext.SPRING_FRAMEWORK_VERSION"
    implementation "org.springframework:spring-jcl:$project.ext.SPRING_FRAMEWORK_VERSION"
    implementation "org.springframework:spring-jdbc:$project.ext.SPRING_FRAMEWORK_VERSION"
    implementation "org.springframework:spring-orm:$project.ext.SPRING_FRAMEWORK_VERSION"
    implementation "org.springframework:spring-test:$project.ext.SPRING_FRAMEWORK_VERSION"
    implementation "org.springframework:spring-tx:$project.ext.SPRING_FRAMEWORK_VERSION"
    implementation "org.springframework:spring-web:$project.ext.SPRING_FRAMEWORK_VERSION"
    implementation "org.springframework:spring-webmvc:$project.ext.SPRING_FRAMEWORK_VERSION"

    implementation "org.mapstruct:mapstruct:$project.ext.MAPSTRUCT_VERSION"
    implementation 'jakarta.mail:jakarta.mail-api:2.1.1'
    testAnnotationProcessor "org.mapstruct:mapstruct-processor:$project.ext.MAPSTRUCT_VERSION"


    implementation group: 'org.glassfish.jaxb', name: 'jaxb-runtime', version: '4.0.5'

    // transitive dependencies called out by libyears

    implementation "org.jetbrains:annotations:24.1.0"
    implementation "commons-logging:commons-logging:1.3.1"

    // jakarta
    implementation "jakarta.servlet:jakarta.servlet-api:6.0.0"
    implementation "jakarta.inject:jakarta.inject-api:2.0.1"
    implementation 'jakarta.mail:jakarta.mail-api:2.1.3'
    implementation 'jakarta.activation:jakarta.activation-api:2.1.3'
    implementation 'jakarta.annotation:jakarta.annotation-api:3.0.0'
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.2'
    implementation 'jakarta.persistence:jakarta.persistence-api:3.1.0'
    implementation 'jakarta.validation:jakarta.validation-api:3.0.2'

    generatedCompile "jakarta.xml.bind:jakarta.xml.bind-api:4.0.1"
    generatedCompile "jakarta.inject:jakarta.inject-api:2.0.1"
    generatedCompile 'jakarta.mail:jakarta.mail-api:2.1.3'
    generatedCompile 'jakarta.annotation:jakarta.annotation-api:3.0.0'
    generatedCompile 'jakarta.persistence:jakarta.persistence-api:3.1.0'
    generatedCompile 'jakarta.validation:jakarta.validation-api:3.0.2'
    generatedCompile 'jakarta.servlet:jakarta.servlet-api:6.0.0'

    implementation 'joda-time:joda-time:2.10'

    generatedCompile 'joda-time:joda-time:2.10'

    // Dependencies for Swagger codegen-generated sources. This should include all dependencies required by Swagger's
    // default okhttp API codegen templates (see https://github.com/swagger-api/swagger-codegen/blob/v2.2.3/samples/client/petstore/spring-stubs/pom.xml)
    // plus any custom templates included in our repo under the SWAGGER_2_TEMPLATE_DIR.
    generatedCompile "org.springframework.boot:spring-boot-starter-data-rest:$project.ext.SPRING_BOOT_VERSION"
    generatedCompile "io.springfox:springfox-swagger-ui:$project.ext.SPRINGFOX_VERSION"
    generatedCompile "org.springframework.boot:spring-boot-starter-validation:$project.ext.SPRING_BOOT_VERSION"
    generatedCompile "com.squareup.okhttp3:okhttp:$project.ext.OKHTTP_VERSION"
    generatedCompile "com.squareup.okhttp3:logging-interceptor:$project.ext.OKHTTP_VERSION"
    generatedCompile "com.google.code.gson:gson:$project.ext.GSON_VERSION"


    // Swagger3 codegen deps
    // TODO(calbach): The duplicated deps here are highly suspect. Investigate
    // removing the generated sourceSet, or else fixing the runtime deps.
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.3'
    implementation 'io.swagger.core.v3:swagger-models:2.2.3'
    implementation 'io.gsonfire:gson-fire:1.8.5'
    implementation 'org.threeten:threetenbp:1.6.2'
    implementation 'io.springfox:springfox-oas:3.0.0'
    generatedCompile 'io.swagger.core.v3:swagger-annotations:2.2.3'
    generatedCompile 'io.swagger.core.v3:swagger-models:2.2.3'
    generatedCompile 'io.gsonfire:gson-fire:1.8.5'
    generatedCompile 'org.threeten:threetenbp:1.6.2'
    generatedCompile 'io.springfox:springfox-oas:3.0.0'

    // war plugin. Add this to scope of the compile configuration, but do not include in .war file.
    providedCompile group: 'jakarta.servlet', name: 'jakarta.servlet-api', version: '6.0.0'

    toolsImplementation 'commons-cli:commons-cli:1.5.0'
    toolsImplementation 'com.opencsv:opencsv:5.7.0'

    testImplementation 'org.mockito:mockito-core:5.3.1'
    testImplementation "com.google.truth:truth:$project.ext.GOOGLE_TRUTH_VERSION"
    testImplementation "com.google.truth.extensions:truth-java8-extension:$project.ext.GOOGLE_TRUTH_VERSION"
    testImplementation 'com.h2database:h2:1.4.194'
    testImplementation "org.liquibase:liquibase-core:$project.ext.LIQUIBASE_VERSION"
    testImplementation 'org.bitbucket.radistao.test:before-after-spring-test-runner:0.1.0'

    // Test deps
    testImplementation("org.springframework.boot:spring-boot-starter-test:$project.ext.SPRING_BOOT_VERSION") {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        exclude group: 'com.vaadin.external.google', module: 'android-json'
    }
}


swaggerSources {
    // Databrowser needs a special configuration style, as we generate Spring server
    // bindings in this case.
    databrowser {
        inputFile = file('src/main/resources/public-api.yaml')
        code {
            configuration = configurations.__swaggerCodegenV3__
            language = 'spring'
            outputDir = file(SWAGGER_3_CODEGEN_DIR)
            templateDir = file(SWAGGER_TEMPLATE_DIR)
            wipeOutputDir = false
            rawOptions = SWAGGER_3_RAW_OPTS
            additionalProperties = [
                    'apiPackage'     : 'org.pmiops.workbench.publicapi',
                    'modelPackage'   : 'org.pmiops.workbench.model',
                    'sourceFolder'   : '',
                    'useTags'        : 'true',
                    // Generates delegate interfaces; used to make method annotations work without
                    // having to copy them to our implementations.
                    'delegatePattern': 'true',
                    'dateLibrary'      : 'java8',
                    'jakarta'          : 'true'
            ]
        }
    }

    databrowserApiClient {
        inputFile = file('src/main/resources/public-api.yaml')
        code {
            configuration = configurations.__swaggerCodegenV3__
            language = 'java'
            outputDir = file(SWAGGER_3_CODEGEN_DIR)
            templateDir = file(SWAGGER_TEMPLATE_DIR)
            wipeOutputDir = false
            rawOptions = SWAGGER_3_RAW_OPTS
            additionalProperties = [
                    'invokerPackage'   : 'org.pmiops.workbench.publicapi.client',
                    'modelPackage'   : 'org.pmiops.workbench.publicapi.client.model',
                    'apiPackage'   : 'org.pmiops.workbench.publicapi.client.api',
                    'library'          : 'okhttp4-gson',
                    'serializableModel': 'true',
                    'dateLibrary'      : 'java8',
                    'jakarta'          : 'true'
            ]
        }
    }

    workbenchClient {
        inputFile = file('src/main/resources/private-workbench.yaml')
        code {
            configuration = configurations.__swaggerCodegenV3__
            language = 'java'
            outputDir = file(SWAGGER_3_CODEGEN_DIR)
            templateDir = file(SWAGGER_TEMPLATE_DIR)
            wipeOutputDir = false
            rawOptions = SWAGGER_3_RAW_OPTS
            additionalProperties = [
                    'invokerPackage'   : 'org.pmiops.workbench.privateworkbench',
                    'modelPackage'     : 'org.pmiops.workbench.privateworkbench.model',
                    'apiPackage'       : 'org.pmiops.workbench.privateworkbench.api',
                    'library'          : 'okhttp4-gson',
                    'serializableModel': 'true',
                    'dateLibrary'      : 'java8',
                    'jakarta'          : 'true'
            ]
        }
    }
}

dependencies {
    modules {
    }
}

tasks.compileJava {
    dependsOn 'compileGeneratedJava'
    options.compilerArgs << '-Xlint:unchecked'
}

tasks.withType(Test) {
    testLogging {
        // Causes the correct line to be reported on an exception.
        exceptionFormat "full"
    }
    def verboseTestLogging=project.properties['verboseTestLogging'] ?: 'no'
    // To debug def verboseTestLogging='yes'
    if (verboseTestLogging == 'yes') {
        testLogging {
            events "passed", "skipped", "failed", "standardOut", "standardError"
        }
    } else {
        testLogging {
            events "passed", "skipped", "failed"
        }
    }
}

test {
    // Starting with a smaller minimum seems to help this task use memory more slowly.
    // Increase max heap size from default of 1024M in order to avoid OOM errors.
    // When Gradle runs out of memory, it fails with this message:
    //   Process 'Gradle Test Executor 1' finished with non-zero exit value 137
    // and literally nothing else in terms of helpful debugging information.
    minHeapSize = '128m'
    maxHeapSize = '2048m'
}

def cloudSdkHomePath = "/google-cloud-sdk"

if (System.getenv('CIRCLECI') == 'true') {
    cloudSdkHomePath = "/home/circleci/google-cloud-sdk"
} else if (System.getenv('PROJECT').equals("stable") || System.getenv('PROJECT').equals("prod")) {
    cloudSdkHomePath = "/root/google-cloud-sdk"
} else {
    cloudSdkHomePath = "/google-cloud-sdk"
}

println "cloudsdkhome"
println cloudSdkHomePath

println "Gradle Version: " + gradle.gradleVersion

appengine {  // App Engine tasks configuration
    tools {
        cloudSdkHome = cloudSdkHomePath
    }
    run {      // local (dev_appserver) configuration (standard environments only)
        port = 8083                 // default
        host = "0.0.0.0"
        // Allows you to attach a debugger on port 8001 when running locally.
        jvmFlags = ['-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=8002']
        // Give dev_appserver 2 minutes to start up when running appengineStart; by
        // default it will fail after 1 minute. (This is particularly a problem in CircleCI.)
        startSuccessTimeout = 120

        environment = [GOOGLE_APPLICATION_CREDENTIALS: file("$rootDir/sa-key.json").getAbsolutePath()]
    }

    // dmohs: You may see this message [1], but don't implement the suggested fix because it breaks
    // Spring.
    // [1] https://github.com/GoogleCloudPlatform/app-gradle-plugin/issues/100
    // stage {
    //    enableJarClasses = true
    // }

    deploy {   // deploy configuration
        stopPreviousVersion = true  // default - stop the current version
        promote = true              // default - & make this the current version
        // TODO(danrodney)
        //account = System.properties("account")
        //project = System.properties("project")
        projectId = "aou-db-test"
    }
}

group = 'org.pmiops.allofus.workbench'
version = '0.1.0'          // Version in generated output

sourceCompatibility = 17
targetCompatibility = 17
